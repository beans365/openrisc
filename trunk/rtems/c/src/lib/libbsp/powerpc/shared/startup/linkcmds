OUTPUT_FORMAT("elf32-powerpc", "elf32-powerpc",
              "elf32-powerpc")
OUTPUT_ARCH(powerpc)
ENTRY(_start)
/* Do we need any of these for elf?
   __DYNAMIC = 0;    */
PROVIDE (__stack = 0);
MEMORY {
	VECTORS	: ORIGIN = 0x0 ,  LENGTH = 0x3000
	CODE : ORIGIN = 0x3000 , LENGTH = 0x100000 
}
SECTIONS
{
  .entry_point_section :
  {
	*(.entry_point_section)
  } > VECTORS

  /*
   * This section is used only if NO_DYNAMIC_EXCEPTION_VECTOR_INSTALL
   * is defined in vectors/vectors.S
   */
  .vectors :
  {
    *(.vectors)
  } > VECTORS

  /* Read-only sections, merged into text segment: */
  .interp   : { *(.interp) }	> CODE
  .hash           : { *(.hash)          }	> CODE
  .dynsym         : { *(.dynsym)                }	> CODE
  .dynstr         : { *(.dynstr)                }	> CODE
  .gnu.version   : { *(.gnu.version)      }	> CODE
  .gnu.version_d   : { *(.gnu.version_d)  }	> CODE
  .gnu.version_r   : { *(.gnu.version_r)  }	> CODE
  .rela.text     :
    { *(.rela.text) *(.rela.gnu.linkonce.t*) }	> CODE
  .rela.data     :
    { *(.rela.data) *(.rela.gnu.linkonce.d*) }	> CODE
  .rela.rodata   :
    { *(.rela.rodata) *(.rela.gnu.linkonce.r*) }	> CODE
  .rela.got       : { *(.rela.got)      }	> CODE
  .rela.got1      : { *(.rela.got1)     }	> CODE
  .rela.got2      : { *(.rela.got2)     }	> CODE
  .rela.ctors     : { *(.rela.ctors)    }	> CODE
  .rela.dtors     : { *(.rela.dtors)    }	> CODE
  .rela.init      : { *(.rela.init)     }	> CODE
  .rela.fini      : { *(.rela.fini)     }	> CODE
  .rela.bss       : { *(.rela.bss)      }	> CODE
  .rela.plt       : { *(.rela.plt)      }	> CODE
  .rela.sdata     : { *(.rela.sdata)    }	> CODE
  .rela.sbss      : { *(.rela.sbss)     }	> CODE
  .rela.sdata2    : { *(.rela.sdata2)   }	> CODE
  .rela.sbss2     : { *(.rela.sbss2)    }	> CODE
  .text      :	
  {
    *(.text)	
    /* .gnu.warning sections are handled specially by elf32.em.  */
    *(.gnu.warning)
    *(.gnu.linkonce.t*)
  }  > CODE
  .init           : { *(.init)          } 	> CODE
  .fini           : { *(.fini)          } 	> CODE
  .rodata         : { *(.rodata) *(.gnu.linkonce.r*) }	> CODE
  .rodata1        : { *(.rodata1) }	> CODE
  _etext = .;
  PROVIDE (etext = .);
  .sdata2   : { *(.sdata2) }	> CODE
  .sbss2   : { *(.sbss2) }	> CODE
  /* Adjust the address for the data segment.  We want to adjust up to
     the same address within the page on the next page up.  It would
     be more correct to do this:
       . = ALIGN(0x40000) + (ALIGN(8) & (0x40000 - 1));
     The current expression does not correctly handle the case of a
     text segment ending precisely at the end of a page; it causes the
     data segment to skip a page.  The above expression does not have
     this problem, but it will currently (2/95) cause BFD to allocate
     a single segment, combining both text and data, for this case.
     This will prevent the text segment from being shared among
     multiple executions of the program; I think that is more
     important than losing a page of the virtual address space (note
     that no actual memory is lost; the page which is skipped can not
     be referenced).  */
  . = ALIGN(0x1000);
 .data    :
  {
    *(.data)
    *(.gnu.linkonce.d*)	
    CONSTRUCTORS
  } > CODE
  .data1   : { *(.data1) }	> CODE
  PROVIDE (__EXCEPT_START__ = .);
  .gcc_except_table   : { *(.gcc_except_table) } > CODE
  PROVIDE (__EXCEPT_END__ = .);
  .got1           : { *(.got1) }	> CODE
  .dynamic        : { *(.dynamic) }	> CODE
  /* Put .ctors and .dtors next to the .got2 section, so that the pointers
     get relocated with -mrelocatable. Also put in the .fixup pointers.
     The current compiler no longer needs this, but keep it around for 2.7.2  */
                PROVIDE (_GOT2_START_ = .);
  .got2           :  { *(.got2) }	> CODE
                PROVIDE (__CTOR_LIST__ = .);
  .ctors          : { *(.ctors) }	> CODE
                PROVIDE (__CTOR_END__ = .);
                PROVIDE (__DTOR_LIST__ = .);
  .dtors          : { *(.dtors) }	> CODE
                PROVIDE (__DTOR_END__ = .);
                PROVIDE (_FIXUP_START_ = .);
  .fixup          : { *(.fixup) }	> CODE
                PROVIDE (_FIXUP_END_ = .);
                PROVIDE (_GOT2_END_ = .);
                PROVIDE (_GOT_START_ = .);
  .got            : { *(.got) }	> CODE
  .got.plt        : { *(.got.plt) }	> CODE
                PROVIDE (_GOT_END_ = .);
  /* We want the small data sections together, so single-instruction offsets
     can access them all, and initialized data all before uninitialized, so
     we can shorten the on-disk segment size.  */
  .sdata          : { *(.sdata) }	> CODE
  _edata  =  .;
  PROVIDE (edata = .);
  .sbss      :
  {
    PROVIDE (__sbss_start = .);
    *(.sbss)
    *(.scommon)
    *(.dynsbss)
    PROVIDE (__sbss_end = .);
  } > CODE
  .plt   : { *(.plt) }	> CODE
  .bss       :
  {
   PROVIDE (__bss_start = .);
   *(.dynbss)
   *(.bss)
   *(COMMON)
  . = ALIGN(16);
  } > CODE
  . = ALIGN(16);
  _end = . ;
  __rtems_end = . ;
  PROVIDE (end = .);
  /DISCARD/ : 
  {
    *(.comment)
  }
}
