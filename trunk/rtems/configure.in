dnl Process this file with autoconf to produce a configure script.
dnl
dnl  $Id: configure.in,v 1.2 2001-09-27 11:59:04 chris Exp $

AC_PREREQ(2.13)
AC_INIT(c)
RTEMS_TOP(.)

# Abort if trying to build inside of the source tree.
if test -f VERSION; then
  rm -f config.cache config.log confdefs.h
  AC_MSG_ERROR([***]
    [Attempt to build inside of the source tree]
    [Please use a separate build directory, instead] )
fi

RTEMS_CANONICAL_TARGET_CPU
AM_INIT_AUTOMAKE(rtems,$RTEMS_VERSION,no)
AM_MAINTAINER_MODE

dnl RTEMS_ENABLE_BARE
dnl RTEMS_ENABLE_RTEMSBSP(rtems_bsp)

dnl These option are only in here to let --help report all supported
dnl options.
RTEMS_ENABLE_MULTIPROCESSING
RTEMS_ENABLE_POSIX
RTEMS_ENABLE_ITRON
RTEMS_ENABLE_NETWORKING
RTEMS_ENABLE_RDBG
RTEMS_ENABLE_INLINES
RTEMS_ENABLE_CXX
RTEMS_ENABLE_GCC28
RTEMS_ENABLE_LIBCDIR
RTEMS_ENABLE_TESTS

RTEMS_CHECK_CPU
RTEMS_CANONICAL_HOST

AC_CONFIG_SUBDIRS(tools/build)
AC_CONFIG_SUBDIRS(tools/update)
AC_CONFIG_SUBDIRS(tools/cpu)

# List all subdirectories to be compiled for the target here
RTEMS_CONFIG_SUBDIRS(c)

if test "$target_alias" != "$host_alias"; then
changequote(,)dnl
TARGET_SUBDIRS=`echo "$target_subdirs" | \
 sed -e "s%\([a-zA-Z0-9][a-zA-Z0-9]*\)%${target_alias}/\1%g"`
changequote([,])dnl
else
TARGET_SUBDIRS="$target_subdirs"
fi
AC_SUBST(TARGET_SUBDIRS)

# Explicitly list all Makefiles here
AC_OUTPUT( 
Makefile
tools/Makefile
make/Makefile
make/custom/Makefile
make/Templates/Makefile
make/compilers/Makefile
doc/Makefile)

RTEMS_OUTPUT_SUBDIRS(RTEMS_TGT_SUBDIRS)
